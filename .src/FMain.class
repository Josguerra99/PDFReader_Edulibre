' Gambas class file

Public pdf As PDF

Public Sub OpenPdf()
  'Se abre una ventana que servira para buscar el archivo pdf que se quiere abrir
  Dialog.Title = "Abrir PDF"
  Dialog.Filter = ["*.pdf;", "Ficheros PDF"]
  Dialog.Path = "."
  If Dialog.OpenFile() Then Return
  'Se abre el pdf
  'Si habia uno abierto anteriormente lo cerramos
  If Not IsNull(pdf) Then
    pdf.Close()  
  Endif
  'Luego de eso ya podemos abrir un nuevo pdf
  pdf = New PDF
  pdf.Init(Dialog.Path)
  
  'Si no se logro abrir bien el pdf entonces lo ponemos como nulo
  If pdf.IsEmpty Then
    ClearUI()
    pdf = Null
    Return    
  Endif
  
  'Dibujamos la primera pagina
  DrawPage(1)
  FMain.Text = pdf.Name
End

Public Sub DrawPage(page As Integer)
  'Si no se a abierto ningun pdf 
  If IsNull(pdf) Then
    Return    
  Endif
  'Se carga la pagina a mostrar y luego se muestra y se cambia el numero de pagina
  pdf.LoadPage(page)
  txtPage.Text = pdf.CurrentPage
  pageRenderer.Picture = pdf.PageImg
  pageRenderer.Resize(pdf.PageImg.Width, pdf.PageImg.Height)  
  'Actualizamos el layout
   Form_Resize()
End

Public Sub GoNext()
  If IsNull(pdf) Then
    Return    
  Endif
  DrawPage(pdf.CurrentPage + 1)
End

Public Sub GoPrevious()
  If IsNull(pdf) Then
    Return    
  Endif
  DrawPage(pdf.CurrentPage - 1)    
End


Public Sub GoToTxtPage()
  DrawPage(CInt(txtPage.Text))
  Catch
    Print "No se puede ir a la pagina " & txtPage.Text
    If Not IsNull(pdf) Then
     txtPage.Text = pdf.CurrentPage
    Endif
End

Public Sub btnOpen_Click() 
  OpenPdf()  
End
Public Sub btnNext_Click() 
  GoNext()
End
Public Sub btnPrevious_Click() 
  GoPrevious()
End

Public Sub txtPage_KeyPress()
  If Key.Code == Key.Enter Or Key.Code == Key.Return Then
    GoToTxtPage()
  Endif    
End

Public Sub Form_Close()
  If Not IsNull(pdf) Then
    pdf.Close()
  Endif  
End


Public Sub ClearUI() 'Quita los elementos si no hay un pdf abierto
pageRenderer.Picture = Null
pageRenderer.Resize(0, 0)
txtPage.Text = ""     
FMain.Text = "PDF Reader"
End


Public Sub Form_Resize() 'Actualizar layout
  'Tama√±o sra ancho del formulario menos el 10% de ese ancho y la altura es el 95% del alto del formulario menos la posicion vertical
  splIndex.Resize(FMain.Width - FMain.Width * 0.1, FMain.Height * 0.95 - splIndex.Y)
  'Se cambia la posicion en x para centrarlo que seria el 5% del ancho del formulario
  splIndex.X = FMain.Width * 0.05
  'Ahora se centra la imagen
  pageRenderer.X = pageRenderer.Parent.Width / 2 - pageRenderer.Width / 2   
End
